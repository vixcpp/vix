@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# ---- deps that exported targets may reference ----
find_dependency(Threads REQUIRED)
find_dependency(fmt CONFIG REQUIRED)
find_dependency(spdlog CONFIG REQUIRED)

if (@VIX_WITH_OPENSSL@)
  find_dependency(OpenSSL REQUIRED)
endif()

if (@VIX_WITH_JSON@)
  find_dependency(nlohmann_json CONFIG REQUIRED)
endif()

if (@VIX_WITH_SQLITE@)
  find_dependency(SQLite3 REQUIRED)

  if (TARGET SQLite::SQLite3)
  elseif (TARGET SQLite3::SQLite3)
    add_library(SQLite::SQLite3 ALIAS SQLite3::SQLite3)
  elseif (TARGET sqlite3)
    add_library(SQLite::SQLite3 ALIAS sqlite3)
  endif()

  if (NOT TARGET SQLite::SQLite3)
    message(FATAL_ERROR "SQLite requested but no usable SQLite target was found.")
  endif()
endif()

# Optional (only if referenced by exported targets)
find_package(MySQLCppConn QUIET CONFIG)
if (NOT TARGET MySQLCppConn::MySQLCppConn)
  add_library(MySQLCppConn::MySQLCppConn INTERFACE IMPORTED)
endif()

if (@VIX_WITH_ZLIB@)
  find_dependency(ZLIB QUIET)
  if (ZLIB_FOUND AND NOT TARGET ZLIB::ZLIB)
    add_library(ZLIB::ZLIB INTERFACE IMPORTED)
    target_include_directories(ZLIB::ZLIB INTERFACE "${ZLIB_INCLUDE_DIRS}")
    target_link_libraries(ZLIB::ZLIB INTERFACE "${ZLIB_LIBRARIES}")
  endif()
  if (NOT TARGET ZLIB::ZLIB)
    message(FATAL_ERROR "Vix built with gzip support but ZLIB::ZLIB is unavailable.")
  endif()
endif()

# ---- load exported targets ----
include("${CMAKE_CURRENT_LIST_DIR}/VixTargets.cmake")

# ---- informational flags ----
set(VIX_HAS_ORM @VIX_HAS_ORM@)
if (VIX_HAS_ORM AND NOT TARGET vix::orm)
  set(VIX_HAS_ORM OFF)
endif()
