@PACKAGE_INIT@

include(CMakeFindDependencyMacro)

# ---- deps that exported targets may reference ----
find_dependency(Threads REQUIRED)
find_dependency(fmt CONFIG REQUIRED)
find_dependency(spdlog CONFIG REQUIRED)

# ---- Boost (required by core/websocket headers: Boost.Beast) ----
if (@VIX_WITH_BOOST@)
  find_dependency(Boost REQUIRED COMPONENTS system)

  # Some environments expose Boost headers as Boost::headers, others as Boost::boost.
  # Ensure Boost::boost exists (headers target).
  if (TARGET Boost::headers AND NOT TARGET Boost::boost)
    add_library(Boost::boost ALIAS Boost::headers)
  endif()

  if (NOT TARGET Boost::boost)
    message(FATAL_ERROR "Boost requested but Boost::boost (headers) target is unavailable.")
  endif()

  if (NOT TARGET Boost::system)
    message(FATAL_ERROR "Boost requested but Boost::system target is unavailable.")
  endif()
endif()

# ---- OpenSSL ----
if (@VIX_WITH_OPENSSL@)
  find_dependency(OpenSSL REQUIRED)
endif()

# ---- JSON backend ----
if (@VIX_WITH_JSON@)
  find_dependency(nlohmann_json CONFIG REQUIRED)
endif()

# ---- SQLite ----
if (@VIX_WITH_SQLITE@)
  find_dependency(SQLite3 REQUIRED)

  # Normalize to SQLite::SQLite3 because many projects use that name.
  if (TARGET SQLite3::SQLite3 AND NOT TARGET SQLite::SQLite3)
    add_library(SQLite::SQLite3 ALIAS SQLite3::SQLite3)
  elseif (TARGET sqlite3 AND NOT TARGET SQLite::SQLite3)
    add_library(SQLite::SQLite3 ALIAS sqlite3)
  endif()

  if (NOT TARGET SQLite::SQLite3)
    message(FATAL_ERROR "SQLite requested but no usable SQLite target was found (SQLite3::SQLite3 / sqlite3).")
  endif()
endif()

# ---- ZLIB (gzip) ----
if (@VIX_WITH_ZLIB@)
  # If Vix was built with gzip support, zlib must be present.
  find_dependency(ZLIB REQUIRED)

  # Some FindZLIB variants set ZLIB_FOUND but don't define ZLIB::ZLIB.
  if (ZLIB_FOUND AND NOT TARGET ZLIB::ZLIB)
    add_library(ZLIB::ZLIB INTERFACE IMPORTED)
    target_include_directories(ZLIB::ZLIB INTERFACE "${ZLIB_INCLUDE_DIRS}")
    target_link_libraries(ZLIB::ZLIB INTERFACE "${ZLIB_LIBRARIES}")
  endif()

  if (NOT TARGET ZLIB::ZLIB)
    message(FATAL_ERROR "Vix built with gzip support but ZLIB::ZLIB is unavailable.")
  endif()
endif()

# ---- MySQL (MUST be resolved BEFORE loading exported targets, if required) ----
set(VIX_DB_WITH_MYSQL @VIX_DB_WITH_MYSQL@)
if (VIX_DB_WITH_MYSQL)

  # 1) Prefer CONFIG package if available
  find_package(MySQLCppConn CONFIG QUIET)

  # 2) Fallback: pkg-config (common on Linux)
  if (NOT TARGET MySQLCppConn::MySQLCppConn)
    find_package(PkgConfig QUIET)
    if (PkgConfig_FOUND)
      pkg_check_modules(_MYSQLCPPCONN QUIET mysqlcppconn)
      if (_MYSQLCPPCONN_FOUND)
        add_library(MySQLCppConn::MySQLCppConn INTERFACE IMPORTED)
        target_include_directories(MySQLCppConn::MySQLCppConn INTERFACE ${_MYSQLCPPCONN_INCLUDE_DIRS})
        target_link_libraries(MySQLCppConn::MySQLCppConn INTERFACE ${_MYSQLCPPCONN_LINK_LIBRARIES})
      endif()
    endif()
  endif()

  # 3) Fallback: raw find_library/find_path
  if (NOT TARGET MySQLCppConn::MySQLCppConn)
    find_path(_MYSQLCPPCONN_INCLUDE_DIR mysql_connection.h
      PATH_SUFFIXES mysqlcppconn mysql
    )
    find_library(_MYSQLCPPCONN_LIBRARY NAMES mysqlcppconn mysqlcppconn8)

    if (_MYSQLCPPCONN_INCLUDE_DIR AND _MYSQLCPPCONN_LIBRARY)
      add_library(MySQLCppConn::MySQLCppConn UNKNOWN IMPORTED)
      set_target_properties(MySQLCppConn::MySQLCppConn PROPERTIES
        IMPORTED_LOCATION "${_MYSQLCPPCONN_LIBRARY}"
        INTERFACE_INCLUDE_DIRECTORIES "${_MYSQLCPPCONN_INCLUDE_DIR}"
      )
    endif()
  endif()

  if (NOT TARGET MySQLCppConn::MySQLCppConn)
    message(FATAL_ERROR
      "Vix was built with MySQL support but MySQLCppConn could not be resolved.\n"
      "Install libmysqlcppconn-dev (Ubuntu) or provide MySQLCppConn_DIR / CMAKE_PREFIX_PATH."
    )
  endif()
endif()

# ---- load exported targets ----
include("${CMAKE_CURRENT_LIST_DIR}/VixTargets.cmake")

# ---- third-party exported targets (aliases) ----
# Re-create ALIAS targets that are used in-tree, because ALIAS targets are not exported by CMake.
if (TARGET vix_thirdparty_asio AND NOT TARGET vix::thirdparty_asio)
  add_library(vix::thirdparty_asio ALIAS vix_thirdparty_asio)
endif()

# ---- informational flags ----
set(VIX_HAS_ORM @VIX_HAS_ORM@)
if (VIX_HAS_ORM AND NOT TARGET vix::orm)
  set(VIX_HAS_ORM OFF)
endif()

set(VIX_HAS_CACHE @VIX_HAS_CACHE@)
if (VIX_HAS_CACHE AND NOT TARGET vix::cache)
  set(VIX_HAS_CACHE OFF)
endif()

set(VIX_WITH_CACHE @VIX_WITH_CACHE@)

# Crypto flags
set(VIX_HAS_CRYPTO @VIX_HAS_CRYPTO@)
if (VIX_HAS_CRYPTO AND NOT TARGET vix::crypto)
  set(VIX_HAS_CRYPTO OFF)
endif()

set(VIX_WITH_CRYPTO @VIX_WITH_CRYPTO@)

set(VIX_HAS_P2P @VIX_HAS_P2P@)
if (VIX_HAS_P2P AND NOT TARGET vix::p2p)
  set(VIX_HAS_P2P OFF)
endif()

set(VIX_WITH_P2P @VIX_WITH_P2P@)

# Optional flags you may export later (kept for forward compatibility)
set(VIX_HAS_TIME @VIX_HAS_TIME@)
if (VIX_HAS_TIME AND NOT TARGET vix::time)
  set(VIX_HAS_TIME OFF)
endif()

set(VIX_WITH_VALIDATION @VIX_WITH_VALIDATION@)
if (VIX_WITH_VALIDATION AND NOT TARGET vix::validation)
  set(VIX_WITH_VALIDATION OFF)
endif()

set(VIX_WITH_WEBRPC @VIX_WITH_WEBRPC@)
if (VIX_WITH_WEBRPC AND NOT TARGET vix::webrpc)
  set(VIX_WITH_WEBRPC OFF)
endif()

set(VIX_WITH_CONVERSION @VIX_WITH_CONVERSION@)
if (VIX_WITH_CONVERSION AND NOT TARGET vix::conversion)
  set(VIX_WITH_CONVERSION OFF)
endif()
